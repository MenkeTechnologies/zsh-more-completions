#compdef s

local arguments
__s_providers(){

    if ! (( $+__s_providers_ary ))  && ! _retrieve_cache s_providers_cache;
    then
        __s_providers_ary=($(_call_program path-all "s -l"))
        _store_cache s_providers_cache __s_providers_ary
    fi

    _wanted s-providers expl s-providers compadd -a -- __s_providers_ary
}
arguments=(
    {-b,--binary}'[binary to launch search URI]'
    {-c,--cert}'[path to cert.pem for TLS]'
    {-k,--key}'[path to key.pem for TLS]'
    {-l,--list-providers}'[list supported providers]'
    '--list-tags[list available tags]'
    {-o,--output}'[output only mode]'
    '--port[server port (default 8080)]'
    {-p,--provider}'[search provider (default "google")]: :__s_providers'
    {-s,--server}'[launch web server]'
    {-t,--tag}'[search tag]'
    {-v,--verbose}'[verbose mode]'
    '--version[display version]'
    '*:filename:_files'
)

_arguments -s $arguments
