#compdef lftp

local arguments

local expl bookmarks XDG_DATA_HOME=${XDG_DATA_HOME:-$HOME/.local/share}

bookmarks=$XDG_DATA_HOME/$service/bookmarks

if [[ -f $bookmarks ]]; then
  bookmarks=(${"${(f)$(<$bookmarks)}"%%[[:space:],]*})
  [[ $service == ncftp ]] && shift 2 bookmarks  
  _wanted bookmarks expl bookmark compadd -a bookmarks && return 0
fi

arguments=(
  '-f[execute commands from the file and exit]:file:_files'
  '-c[execute the commands and exit]:command:_path_commands'
  '--norc[dont execute rc files from the home directory]'
  '(- * :)'{-h,--help}'[print this help and exit]'
  '(- * :)--version[print lftp version and exit]'
  '-e[execute the command just after selecting]'
  '-u[use the user/password for authentication]:user:_users'
  '-p[use the port for connection]:port:_ports'
  '-s[assign the connection to this slot]'
  '-d[switch on debugging mode]'
  '*:url:_urls'
)

_arguments -s $arguments
